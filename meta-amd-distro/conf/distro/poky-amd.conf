require conf/distro/poky.conf

DISTRO = "poky-amd"
DISTRO_NAME = "Poky for AMD"
DISTROOVERRIDES = "${DISTRO}"
TARGET_VENDOR = "-amd"
SDK_VENDOR = "-amdsdk"
MAINTAINER = "Wade Farnsworth <wade_farnsworth@mentor.com>"

DISTRO_FEATURES_BACKFILL_CONSIDERED = "sysvinit"
VIRTUAL-RUNTIME_initscripts = ""
DISTRO_FEATURES_append = " systemd"
VIRTUAL-RUNTIME_init_manager = "systemd"

EXTRA_IMAGE_FEATURES_append = " package-management ssh-server-openssh"
PACKAGE_CLASSES = "package_deb"

DISTRO_EXTRA_RDEPENDS += "connman udev-extraconf"

IMAGE_ROOTFS_EXTRA_SPACE = "51200"

# We want information about package and image contents
INHERIT += "buildhistory"
BUILDHISTORY_COMMIT ?= "1"

# IMAGE_FSTYPES "live" is enabled by default in oe-core which is
# responsible for building both "iso" & "hddimg". Although the
# right way to specify which ones to build is using NOISO & NOHDD
# vars, there's a specific scenario where Toaster build artifacts
# collection mechanism depends on the IMAGE_FSTYPES variable to
# specify the image types explicitly (See: commit a5b1aa4).
# Also, "iso" doesn't get built even if specified explicitly
# because NOISO = "1" by default (See c787dcd in oe-core).
# Therefore, we do as follows:
IMAGE_FSTYPES += "wic wic.bmap iso"
NOISO = "0"
NOHDD = "1"

WKS_FILES ?= "amd-usbimage.wks"

# Contents of IMAGE_BOOT_FILES are deployed into "amd-usbimage.wks" wic image
IMAGE_BOOT_FILES = " \
        ${EFIIMGDIR}${EFIDIR}/*;EFI/BOOT/ \
        ${EFIIMGDIR}/startup.nsh;startup.nsh \
        ${KERNEL_IMAGETYPE} \
        ${INITRD_IMAGE_LIVE}-${MACHINE}.cpio.gz;initrd \
        ${ROOTFS};rootfs.img \
"

# This makes sure that boot files are deployed before do_image_wic starts execution
do_image_wic[depends] += "${IMAGE_BASENAME}:do_bootimg"

IMAGE_OVERHEAD_FACTOR = "1.1"
